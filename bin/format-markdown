#!/usr/bin/env perl

use v5.18;

use Encode qw(decode encode);

my $lines = 0;
my @line = ();
my $line = '';

sub in {
  decode('UTF-8', <>, Encode::FB_CROAK);
}
sub out {
  my ($out) = @_;
  print encode('UTF-8', $out, Encode::FB_CROAK);
}
sub peek {
  $_ = @line ? $line[0] : '';
}
sub take {
  $_ = shift(@line);
}
sub poke {
  unshift @line, @_ ? (shift) : $_;
  $_ = @line ? $line[0] : '';
}
sub give {
  unshift(@line, "$_[0]\n");
}
sub space {
  out take while peek eq "\n";
  out take if @line == 1;
  return 1;
}


sub lines {
  my ($p, $m, $f) = @_;
  $p //= '';
  $m //= 79;
  $f //= 1;
  while (my $s = $line) {
    my $m2 = $m - 2;
    my $m3 = $m - 4;
    if (length $line <= $m) {
      out "$p$line\n";
      last;
    }
    elsif ($line =~ s{^(\ \ \*\ .{10,$m3}\S)(?:$|\ (?=\S))}{}) {
      out "$p$1\n";
    }
    elsif ($line =~ s{^(.{5,$m2}\ \ )$/}{}) {
      out "$p$1\n";
    }
    elsif ($line =~ s{^(\S.{10,$m2}\S)(?:$|\ (?=\S))}{}) {
      out "$p$1\n";
    }
    else {
      die ">>$line<<";
    }
    if ($f) {
      $f = 0;
      if ($s =~ /^\*\ /) {
        $p = '  ';
        $m -= 2;
      }
      elsif ($s =~ /^\ {2}\*\ /) {
        $p = '    ';
        $m -= 4;
      }
      elsif ($s =~ /^\ /) {
        die "Unsupported line '$s'";
      }
    }
  }
}

sub wrap {
  my $end = shift;
  my @line;
  while (peek =~ /./ and peek !~ /^:/) {
    my $line = take;
    chomp $line;
    $line =~ s/^\ +(?=\S)(?!\*)//;
    push @line, $line;
    last if $end->($line);
  }
  $line = join ' ', @line;
  $line =~ s{\]\(\ }{](};
  lines;
  space;
  return 1;
}

sub lis2 {
  my @line;
  while (peek =~ /^\ \ \S/) {
    my $line = take;
    chomp $line;
    $line =~ s/^\ \ //;
    push @line, $line;
    last if $line =~ /(  |\.)$/;
  }
  $line = join ' ', @line;
  $line =~ s{\]\(\ }{](};
  lines('  ', 77, 0);
  space;
  return 1;
}

sub titl {
    out take;
    out take;
    space;
}

sub head {
  out take;
  out take if peek =~ /^\{:/;
  space;
}

sub line {
  out take;
}

sub para {
  my $end = sub { $_[0] =~ /(  |[\.\!\?])$/ };
  wrap $end;
}

sub ddef {
  out take;
}

sub list {
  my $end = sub { peek =~ /^(?:\s{0,2}\*\ |$)/ };
  wrap($end);
}

sub toc {
  line;
  line;
  space;
}

sub pre {
  line;
  line while peek !~ /^\`\`\`$/;
  line;
  line while peek =~ /^<!-- .* -->$/;
  space;
}

sub tabl {
  line while peek =~ /^\|\ /;
  space;
}

sub leg {
  line;
  line while peek =~ /^\*\ /;
  space;
}

sub comm {
  my $line = take;
  out $line;
  if ($line !~ /-->/) {
    while (peek !~ /-->$/) {
      out take;
    }
    out take;
  }
  space;
}

sub unknown {
  my $line = take;
  my $show = $line;
  chomp $show;
  my $num = $lines - @line + 1;
  warn "Unrecognized line (#$num): >>$show<<\n";
  out $line;
  space;
}

#------------------------------------------------------------------------------
sub main {
  push @line, $_ while $_ = in;
  $lines = @line;

  if (peek =~ /^---$/) {
    out take;
    out take while peek !~ /^---$/;
    out take;
    space;
  }

  while (my $peek = peek) {
    titl and next if @line >= 2 and $line[1] =~ /^===+$/;
    head and next if $peek =~ /^#+\ /;
    line and next if $peek =~ /^\[\^/;
    line and space and next if $peek =~ /^\[\#\S+\]$/;
    leg  and next if $peek =~ /^\*\*Legend:\*\*$/;
    toc  and next if $peek =~ /^\{:toc\}$/;
    pre  and next if $peek =~ /^\`\`\`$/;
    list and next if $peek =~ /^\ {0,2}\*\ /;
    lis2 and next if $peek =~ /^\ \ \S/;
    tabl and next if $peek =~ /^\|\ /;
    comm and next if $peek =~ /^<!--/;
    ddef and next if $peek =~ /^:\n/;
    para and next if $peek =~ /^(
      [\w\#\*"`] |
      \[\w |
      \!\[ |
      \(\w |
      \>\ |
      \[\"\*\*
    )/x;

    unknown;
  }
}

main(@ARGV);
